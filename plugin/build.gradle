plugins {
    id 'idea'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '2.0.0'
    id 'com.gradleup.shadow' version '9.2.2'
}

group = 'com.sb.jini'
version = '0.2'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.13.2'
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'
    testCompileOnly 'org.projectlombok:lombok:1.18.42'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.42'
    testImplementation("org.assertj:assertj-core:3.27.6")
    testImplementation(platform('org.junit:junit-bom:6.0.1'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testRuntimeOnly('org.junit.platform:junit-platform-launcher')
}

testing {
    suites {
        test {
            useJUnitJupiter()
        }
    }
}

gradlePlugin {
    plugins {
        jini {
            id = 'com.sb.jini'
            displayName = 'Jini Gradle Plugin'
            description = 'This plugin automate creation of executables for Windows using JINI - simple jvm-starter'
            website = 'https://github.com/bochkov/jini-gradle-plugin'
            vcsUrl = 'https://github.com/bochkov/jini-gradle-plugin.git'
            tags = ['windows', 'exe']
            implementationClass = 'com.sb.jini.JiniPlugin'
        }
    }
}

tasks.named('shadowJar', com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    enableAutoRelocation = true
    archiveClassifier = ''
}

publishing {
    repositories {
        maven {
            name = 'reposilite'
            url = 'https://maven.sergeybochkov.com/gradle-plugins'
            credentials {
                username = "$reposilite_user"
                password = "$reposilite_password"
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}